# apps/examples/react1
#
#
# This is a simple example showing how to construct a single page React app.

# ----- CSS -------------------------------------------------------------------------------
# Any CSS you add here will be applied to the page
: CSS   '''
h1 {
    color: blue;
}
''';

# ----- JSX -------------------------------------------------------------------------------
# You can define React components on-the-fly in JSX
: JSX   '''
''';

# ----- REACT-FORTHIC -------------------------------------------------------------------------------
# This is where the app comes together
: REACT-FORTHIC   '''
# Some convenience words for creating elements
# NOTE that FunctionSample and ClassSample are defined in the JSX word above
: DIV                       "'div'" Element;
: H1                        "'h1'" Element;
: P                         "'p'" Element;

# ===== HOME-ELEMENT
# The HOME-ELEMENT shows how to explicitly assemble React elements using Forthic code
# The <CONTENT word is used to set the content of the React element.
: HOME-ELEMENT
   DIV [
        H1 "My Heading" <CONTENT
        P "My first paragraph" <CONTENT
    ] <CONTENT
;

# ----- Routes and router
# The Route word constructs a Route that serves a specified element at the specified path. For
# this example, we'll just have a single route for the root of the app
: HOME-ROUTE   "/" HOME-ELEMENT Route;

# The MAIN-ROUTER word is what gets called to construct and mount your app. We construct a Router
# by passing an array of routes that it can render.
: MAIN-ROUTER   [HOME-ROUTE] Router;
''';

# ----- Server Forthic -----------------------------------------------------------------------------
# The only thing we need to add here is a MAIN-PAGE word so the server can render the content to send
# to the browser. If MAIN-PAGE returns a string, that string will be rendered directly; if it returns
# an array, it expects the first element to describe what needs to be rendered, followed by any
# additional info. Here we render a v1 React App using the words defined above.
: MAIN-PAGE   ["REACT-APP/V1"  CSS JSX REACT-FORTHIC];